plugins {
    id 'java'
    id 'io.gatling.gradle' version "3.5.1"
    id "org.zeroturnaround.gradle.jrebel" version "1.1.10"
}

def xms = System.getenv("JAVA_XMS_SIZE") ?: "512M"
def xmx = System.getenv("JAVA_XMX_SIZE") ?: "512M"

def hostName = System.getenv("HOSTNAME") ?: "localhost"
def bucketName = System.getenv("RESULTS_BUCKET_NAME") ?: "nome-do-bucket"

group 'br.com.gatling'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    gatling platform(group: 'com.amazonaws', name: 'aws-java-sdk-bom', version: '1.11.1022')
    gatling group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.1022'
    gatling group: 'com.typesafe.play', name: 'play-json_2.13', version: '2.10.0-RC2'
    gatling group: 'com.fasterxml.jackson.module', name: 'jackson-module-scala_2.9.1', version: '2.1.3'
    gatling group: 'org.zeroturnaround', name: 'zt-zip', version: '1.12'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

gatling {
    jvmArgs = ['-server', '-XX:+UseThreadPriorities',
               '-XX:ThreadPriorityPolicy=42',
               "-Xms${xms}", "-Xmx${xmx}", '-Xmn100M',
               '-XX:+HeapDumpOnOutOfMemoryError',
               '-XX:+AggressiveOpts',
               '-XX:+OptimizeStringConcat',
               '-XX:+UseFastAccessorMethods',
               '-XX:+UseParNewGC',
               '-XX:+UseConcMarkSweepGC',
               '-XX:+CMSParallelRemarkEnabled',
               '-Djava.net.preferIPv4Stack=true',
               '-Djava.net.preferIPv6Addresses=false']
    simulations = { include "**/*Simulation.scala" }
}

test {
    useJUnitPlatform()
}

task uploadLogs(type: JavaExec, dependsOn: compileGatlingScala) {
    main = 'br.com.util.LogUploader'
    classpath = sourceSets.gatling.runtimeClasspath
}

task downloadLogs(type: JavaExec, dependsOn: compileGatlingScala) {
    main = 'br.com.util.LogDownloader'
    classpath = sourceSets.gatling.runtimeClasspath
}

task buildReport(type: JavaExec, dependsOn: downloadLogs) {
    main = 'io.gatling.app.Gatling'
    classpath = sourceSets.gatling.runtimeClasspath
    args = [
            "-ro", "build/reports/downloadedLogs",
            "-rf", ""
    ]
}

task uploadReport(type: JavaExec, dependsOn: buildReport) {
    main = 'br.com.util.ReportUploader'
    classpath = sourceSets.gatling.runtimeClasspath
}

task deleteLogs(type: JavaExec, dependsOn: uploadReport) {
    main = 'br.com.util.LogDeleter'
    classpath = sourceSets.gatling.runtimeClasspath
}